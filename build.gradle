apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework'
    version = '6.3.2'
}

def elasticVersion = '6.2.4'
def kafkaVersion = '1.1.0'
def jacksonVersion = '2.9.5'
def junitVersion = '5.2.0'
def mockitoVersion = '2.18.3'
def assertjVersion = '3.9.1'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-api")
        api 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.javassist:javassist:3.22.0-GA'
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
        implementation 'org.apache.httpcomponents:httpclient:4.5.5'
        implementation 'io.undertow:undertow-core:2.0.7.Final'
        compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.0'
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        api "org.mockito:mockito-core:${mockitoVersion}"
        api "org.assertj:assertj-core:${assertjVersion}"
        implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.0'
    }
}

project(':core-ng-mongo') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api 'org.mongodb:mongo-java-driver:3.5.0'
        testImplementation project(":core-ng-test")
        testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    }
}

project(':core-ng-mongo-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-test")
        api project(":core-ng-mongo")
        implementation 'com.github.fakemongo:fongo:2.1.1'
    }
}

project(':core-ng-search') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.elasticsearch.client:transport:${elasticVersion}"
        testImplementation project(":core-ng-test")
    }
}

project(':core-ng-search-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-test")
        api project(":core-ng-search")
        implementation "org.codelibs.elasticsearch.module:lang-painless:${elasticVersion}"
        implementation "org.codelibs.elasticsearch.module:mapper-extras:${elasticVersion}"     // used by elasticsearch scaled_float
    }
}

def mavenURL = hasProperty('mavenURL') ? mavenURL : null    // usage: "gradlew -PmavenURL=/path clean publish"

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'
    if (mavenURL != null) {
        assert project.file(mavenURL).exists()
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar { classifier 'sources' }
                }
            }
            repositories {
                maven { url mavenURL }
            }
        }
    }
}

project(':log-processor') {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        compile project(':core-ng')
        compile project(':core-ng-search')
        compile "org.apache.kafka:kafka-clients:${kafkaVersion}"
        testCompile project(':core-ng-test')
        testCompile project(':core-ng-search-test')
    }
}
